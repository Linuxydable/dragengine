/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.LoadSave
pin Dragengine.Utils



/**
 * \brief Conversation condition determining if actor is in a conversation.
 */
class CConditionActorInConversation extends ConversationCondition
	/** \brief Factory for loading actor actions. */
	public class Factory implements PersistencyFactory
		public static fixed var String NAME = "CConditionActorInConversation"
		
		public func new()
		end
		
		public func Persistable readObjectFromFile( PersistencyEnvironment env, FileReader reader )
			return CConditionActorInConversation.new( env, reader )
		end
		
		public static func void registerFactory( Persistency persistency )
			persistency.addFactory( NAME, Factory.new() )
		end
	end
	
	
	
	private var StringID pActorID
	private var bool pNegate
	
	
	
	/** \brief Create conversation condition. */
	public func new()
		this.pNegate = false
	end
	
	/** \brief Read from file. */
	public func new( PersistencyEnvironment env, FileReader reader )
		select reader.readByte() // version
		case 0
			this.pActorID = env.readStringIDOrNullIfEmpty( reader )
			this.pNegate = env.readBool( reader )
			
		else
			throw EInvalidParam.new( "unsupported version" )
		end
	end
	
	
	
	/** \brief ID of actor to check. */
	public func StringID getActorID()
		return this.pActorID
	end
	
	/** \brief Set actor ID. */
	public func void setActorID( StringID id )
		if id == null
			throw EInvalidParam.new()
		end
		
		this.pActorID = id
	end
	
	/** \brief Result of condition is negated. */
	public func bool getNegate()
		return this.pNegate
	end
	
	/** \brief Set if result of condition is negated. */
	public func void setNegate( bool negate )
		this.pNegate = negate
	end
	
	
	
	/** \brief Evaluate condition. */
	public func bool evaluate( ConversationPlayback playback )
		var ConversationActor actor = playback.getActorMatching( this.pActorID )
		if actor == null
			actor = playback.loadConversationActor( this.pActorID )
		end
		
		if this.pNegate
			return actor == null or actor.getPlayback() == null
			
		else
			return actor != null and actor.getPlayback() != null
		end
	end
	
	
	
	/**
	 * \brief Name of PersistencyFactory required to load object from file.
	 * 
	 * Name of PersistencyFactory is first written to the file as string with
	 * 8-bit length. Then writeToFile() is called to write the object itself.
	 * Empty name is not allowed as is names longer than 255 characters.
	 * Name has to match a PersistencyFactory present in the Persistency
	 * instance used for loading objects. Object is read using
	 * PersistencyFactory.readObjectFromFile() of the matching PersistencyFactory.
	 */
	public func String persistencyFactoryName()
		return Factory.NAME
	end
	
	/** \brief Write object to file writer. */
	public func void writeObjectToFile( PersistencyEnvironment env, FileWriter writer )
		writer.writeByte( 0 ) // version
		
		env.writeStringIDOrEmptyIfNull( writer, this.pActorID )
		env.writeBool( writer, this.pNegate )
	end
end
