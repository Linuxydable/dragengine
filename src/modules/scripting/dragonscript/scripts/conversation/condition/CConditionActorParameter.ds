/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.LoadSave
pin Dragengine.Utils



/**
 * \brief Conversation condition comparing value of actor parameter.
 * 
 * The default is equal as operator and 0 as test value. If no parameter
 * with the given name exists the value 0 is used as the value of the parameter.
 */
class CConditionActorParameter extends ConversationCondition
	/** \brief Factory for loading actor actions. */
	public class Factory implements PersistencyFactory
		public static fixed var String NAME = "CConditionActorParameter"
		
		public func new()
		end
		
		public func Persistable readObjectFromFile( PersistencyEnvironment env, FileReader reader )
			return CConditionActorParameter.new( env, reader )
		end
		
		public static func void registerFactory( Persistency persistency )
			persistency.addFactory( NAME, Factory.new() )
		end
	end
	
	
	
	/** \brief Compare operators. */
	public enum Operator
		/** \brief True if the parameter value is equal to the test value. */
		equal
		
		/** \brief True if the parameter value is not equal to the test value. */
		notEqual
		
		/** \brief True if the parameter value is less than the test value. */
		lessThan
		
		/** \brief True if the parameter value is less than or equal to the test value. */
		lessThanOrEqual
		
		/** \brief True if the parameter value is greater than the test value. */
		greaterThan
		
		/** \brief True if the parameter value is greater or equal to the test value. */
		greaterThanOrEqual
	end
	
	
	
	private var StringID pActorID
	private var Operator pOperator
	private var String pParameter
	private var int pTestValue
	private var String pTestVariable
	
	
	
	/** \brief Create conversation condition. */
	public func new()
		this.pOperator = Operator.equal
		this.pTestValue = 0
	end
	
	/** \brief Read from file. */
	public func new( PersistencyEnvironment env, FileReader reader )
		select reader.readByte() // version
		case 0
			this.pActorID = env.readStringIDOrNullIfEmpty( reader )
			this.pOperator = Operator.withOrder( reader.readByte() )
			this.pParameter = env.readString8OrNullIfEmpty( reader )
			this.pTestValue = reader.readInt()
			this.pTestVariable = env.readString8OrNullIfEmpty( reader )
			
		else
			throw EInvalidParam.new( "unsupported version" )
		end
	end
	
	
	
	/** \brief Actor ID or null if not set. */
	public func StringID getActorID()
		return this.pActorID
	end
	
	/** \brief Set actor ID or null if not set. */
	public func void setActorID( StringID id )
		this.pActorID = id
	end
	
	/** \brief Operator. */
	public func Operator getOperator()
		return this.pOperator
	end
	
	/** \brief Set operator. */
	public func void setOperator( Operator operator )
		this.pOperator = operator
	end
	
	/** \brief Name of the parameter. */
	public func String getParameter()
		return this.pParameter
	end
	
	/** \brief Set name of the parameter. */
	public func void setParameter( String parameter )
		this.pParameter = parameter
	end
	
	/** \brief Test value. */
	public func int getTestValue()
		return this.pTestValue
	end
	
	/** \brief Set test value. */
	public func void setTestValue( int value )
		this.pTestValue = value
	end
	
	/** \brief Test variable or null to use test value. */
	public func String getTestVariable()
		return this.pTestVariable
	end
	
	/** \brief Set test variable or null to use test value. */
	public func void setTestVariable( String variable )
		this.pTestVariable = variable
	end
	
	
	
	/** \brief Condition is true. */
	public func bool evaluate( ConversationPlayback playback )
		var ConversationActor actor = playback.getActorMatching( this.pActorID )
		
		var int parameterValue = 0
		if actor != null and this.pParameter != null
			parameterValue = actor.getParameter( this.pParameter )
		end
		
		var int testValue = this.pTestValue
		if this.pTestVariable != null
			testValue = playback.getVariable( this.pTestVariable, testValue )
		end
		
		select this.pOperator
		case Operator.equal
			return parameterValue == testValue
			
		case Operator.notEqual
			return parameterValue != testValue
			
		case Operator.lessThan
			return parameterValue < testValue
			
		case Operator.lessThanOrEqual
			return parameterValue <= testValue
			
		case Operator.greaterThan
			return parameterValue > testValue
			
		case Operator.greaterThanOrEqual
			return parameterValue >= testValue
			
		else
			throw EInvalidParam.new()
		end
	end
	
	
	
	/**
	 * \brief Name of PersistencyFactory required to load object from file.
	 * 
	 * Name of PersistencyFactory is first written to the file as string with
	 * 8-bit length. Then writeToFile() is called to write the object itself.
	 * Empty name is not allowed as is names longer than 255 characters.
	 * Name has to match a PersistencyFactory present in the Persistency
	 * instance used for loading objects. Object is read using
	 * PersistencyFactory.readObjectFromFile() of the matching PersistencyFactory.
	 */
	public func String persistencyFactoryName()
		return Factory.NAME
	end
	
	/** \brief Write object to file writer. */
	public func void writeObjectToFile( PersistencyEnvironment env, FileWriter writer )
		writer.writeByte( 0 ) // version
		
		env.writeStringIDOrEmptyIfNull( writer, this.pActorID )
		writer.writeByte( this.pOperator.order() )
		env.writeString8OrEmptyIfNull( writer, this.pParameter )
		writer.writeInt( this.pTestValue )
	end
end
