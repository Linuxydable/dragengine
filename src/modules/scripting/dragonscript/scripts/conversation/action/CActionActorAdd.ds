/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Conversation action adding an actor to the conversation if not there yet.
 */
class CActionActorAdd extends ConversationAction
	/** \brief Factory for loading actor actions. */
	public class Factory implements PersistencyFactory
		public static fixed var String NAME = "CActionActorAdd"
		
		public func new()
		end
		
		public func Persistable readObjectFromFile( PersistencyEnvironment env, FileReader reader )
			return CActionActorAdd.new( env, reader )
		end
		
		public static func void registerFactory( Persistency persistency )
			persistency.addFactory( NAME, Factory.new() )
		end
	end
	
	
	
	private var StringID pActorID
	private var StringID pAliasID
	
	
	
	/** \brief Create conversation action. */
	public func new()
	end
	
	/** \brief Read from file. */
	public func new( PersistencyEnvironment env, FileReader reader )
		this.pActorID = env.readStringIDOrNullIfEmpty( reader )
		this.pAliasID = env.readStringIDOrNullIfEmpty( reader )
	end
	
	
	
	/** \brief Actor ID or null if not set. */
	public func StringID getActorID()
		return this.pActorID
	end
	
	/** \brief Set actor ID or null if not set. */
	public func void setActorID( StringID id )
		this.pActorID = id
	end
	
	/** \brief Optional alias ID to set for the actor or null if not set. */
	public func StringID getAliasID()
		return this.pAliasID
	end
	
	/** \brief Set optional alias ID to set for the actor or null if not set. */
	public func void setAliasID( StringID id )
		this.pAliasID = id
	end
	
	
	
	/** \brief Execute action. */
	public func void execute( ConversationPlayback playback )
		var ConversationActor actor = playback.getActorMatching( this.pActorID )
		
		if actor == null
			actor = playback.loadConversationActor( this.pActorID )
			playback.addActor( actor, this.pAliasID )
		end
		
		playback.advanceToNextAction()
	end
	
	
	
	/**
	 * \brief Name of PersistencyFactory required to load object from file.
	 * 
	 * Name of PersistencyFactory is first written to the file as string with
	 * 8-bit length. Then writeToFile() is called to write the object itself.
	 * Empty name is not allowed as is names longer than 255 characters.
	 * Name has to match a PersistencyFactory present in the Persistency
	 * instance used for loading objects. Object is read using
	 * PersistencyFactory.readObjectFromFile() of the matching PersistencyFactory.
	 */
	public func String persistencyFactoryName()
		return Factory.NAME
	end
	
	/** \brief Write object to file writer. */
	public func void writeObjectToFile( PersistencyEnvironment env, FileWriter writer )
		env.writeStringIDOrEmptyIfNull( writer, this.pActorID )
		env.writeStringIDOrEmptyIfNull( writer, this.pAliasID )
	end
end
