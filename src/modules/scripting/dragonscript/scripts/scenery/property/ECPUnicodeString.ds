/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Utils
pin Dragengine.Preloading



/**
 * \brief UnicodeString value element class property for use with XML loading.
 */
class ECPUnicodeString extends ElementClassProperty
	/** \brief Value. */
	private var UnicodeString pValue
	
	
	
	/** \brief Create property. */
	public func new( String name, UnicodeString value ) super( name )
		this.pValue = value
	end
	
	/** \brief Create property. */
	public func new( String name, String value ) super( name )
		this.pValue = UnicodeString.newFromUTF8( value )
	end
	
	
	
	/** \brief Value. */
	public func UnicodeString getValue()
		return this.pValue
	end
	
	/** \brief Set value. */
	public func void setValue( UnicodeString value )
		this.pValue = value
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue( Object value )
		if value castable UnicodeString
			this.pValue = value cast UnicodeString
			
		else
			this.pValue = UnicodeString.newFromUTF8( value cast String )
		end
	end
	
	
	
	/**
	 * \brief String value value modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored value is returned.
	 * If the named stub property is present it is returned.
	 */
	public func UnicodeString getValue( StubWithProperties stub )
		var String value = stub.getPropertyValueFor( this.getName(), null )
		if value != null
			return UnicodeString.newFromUTF8( value )
			
		else
			return this.pValue
		end
	end
end
