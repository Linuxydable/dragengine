/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.Preloading


/**
 * \brief Spawn point for actors and props.
 * 
 * Spawn points have a unique name and are added to the SpawnPointList of a GameWorld.
 */
class SpawnPointClass extends ElementClass
	/** \brief Name. */
	private var ECPString pName
	
	
	
	/** Creates a new SpawnPointClass object. */
	public func new() this( "SpawnPoint" )
	end
	
	/** Creates a new SpawnPointClass object. */
	public func new( String className ) super( className )
		this.pName = ECPString.new( "name", null )
		this.addProperty( this.pName )
		this.setPersistable( false )
	end
	
	
	
	/** \brief Name. */
	public func ECPString getName()
		return this.pName
	end
	
	
	
	/**
	 * \brief Create element from given stub and add it to game world.
	 * 
	 * Adds spawn point to game world.
	 */
	public func void createAndAddElement( StubElement stub, GameWorld gameWorld )
		var SpawnPoint spawnPoint = SpawnPoint.new( this.pName.getValue( stub ) )
		
		spawnPoint.setPosition( stub.getPosition() )
		spawnPoint.setOrientation( Quaternion.newFromEuler( stub.getRotation() ) )
		
		gameWorld.getSpawnPoints().add( spawnPoint )
	end
end
