/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.Scenery
pin Dragengine.Gui
pin Dragengine.Preloading


/**
 * \brief Corner in a Rail element class.
 */
class RailCornerClass extends ElementClass
	private var ECPStringID pRailID
	private var ECPInteger pOrder
	
	
	
	/** \brief Create element class. */
	public func new() super( "RailCorner" )
		this.setPersistable( false )
		
		this.pRailID = ECPStringID.new( "id" )
		this.addProperty( this.pRailID )
		
		this.pOrder = ECPInteger.new( "order", 0 )
		this.addProperty( this.pOrder )
	end
	
	
	
	/**
	 * \brief Create rail corner adding it to game world.
	 */
	public func void createAndAddElement( StubElement stub, GameWorld gameWorld )
		var RailCorner corner = this.createRailCorner( this.pOrder.getValue( stub ) )
		corner.setPosition( stub.getPosition() )
		corner.setOrientation( Quaternion.newFromEuler( stub.getRotation() ) )
		gameWorld.getRailList().getOrAddWithID( this.pRailID.getValue( stub ) ).addCorner( corner )
	end
	
	/**
	 * \brief Create rail corner.
	 * 
	 * Default implementation creates instance of RailCorner. Overwrite in subclass to
	 * create rail corner of different subclass.
	 */
	protected func RailCorner createRailCorner( int order )
		return RailCorner.new( order )
	end
end

