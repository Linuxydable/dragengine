/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Gui



/**
 * \brief Toggle model.
 * 
 * Switches between an activated and deactivated state.
 */
class DefaultToggleModel implements ToggleModel
	private var bool pToggled
	private var Array pListeners
	
	
	
	/** \brief Create toggle model with false toggled state. */
	public func new() this( false )
	end
	
	/** \brief Create toggle model. */
	public func new( bool toggled )
		this.pToggled = toggled
		this.pListeners = Array.new()
	end
	
	
	
	/** \brief Get toggled state. */
	public func bool getToggled()
		return this.pToggled
	end
	
	/** \brief Set toggled state. */
	public func void setToggled( bool toggled )
		if toggled == this.pToggled
			return
		end
		
		this.pToggled = toggled
		
		this.pListeners.forEach( block ToggleModelListener each
			each.toggledChanged( this )
		end )
	end
	
	/** \brief Switch toggled state. */
	public func void toggle()
		this.setToggled( not this.pToggled )
	end
	
	
	
	/**
	 * \brief Add listener.
	 * \throws ENullPointer \em listener is \em null.
	 * \throws EInvalidParam \em listener is present.
	 */
	public func void addListener( ToggleModelListener listener )
		if listener == null
			throw ENullPointer.new()
		end
		this.pListeners.add( listener )
	end
	
	/**
	 * \brief Remove listener.
	 * \throws EInvalidParam \em listener is absent.
	 */
	public func void removeListener( ToggleModelListener listener )
		this.pListeners.remove( listener )
	end
end
